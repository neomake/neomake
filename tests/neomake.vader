" Main entry point to run all Vader tests.
" You can run the included files by itself (since they also include
" include/setup.vader).
Include: include/setup.vader

~ Features
Include (Error handling): errors.vader
Include (Hooks): hooks.vader
Include (Integration tests): integration.vader
Include (Lists): lists.vader
Include (Maker args): args.vader
Include (Makers): makers.vader
Include (Mapexpr): mapexpr.vader
Include (Processing): processing.vader
Include (Serialize): serialize.vader
Include (Signs): signs.vader
Include (Statusline): statusline.vader
Include (Utils): utils.vader

~ Filetype specific
Include (Haskell): ft_haskell.vader
Include (Perl): ft_perl.vader
Include (Python): ft_python.vader
Include (Shell): ft_sh.vader
Include (Text): ft_text.vader
Include (Elixir): ft_elixir.vader
Include (PHP): ft_php.vader

* Old/unorganized tests
Execute (neomake#GetMakers):
  AssertEqual neomake#GetMakers('non-existent'), []
  AssertEqual neomake#GetMakers('pug'), ['puglint']
  AssertEqual sort(neomake#GetMakers('sh')), ['checkbashisms', 'sh', 'shellcheck']

Execute (neomake#GetMaker with non-existent maker prints errors):
  AssertEqual neomake#GetMaker('non-existent'), {}
  AssertEqual g:neomake_test_messages[-1], [0, 'Invalid maker name: non-existent', {}]
  AssertEqual neomake#GetMaker('nonexistent'), {}
  AssertEqual g:neomake_test_messages[-1], [0, 'Maker not found: nonexistent', {}]

Execute (Neomake! reports error on execution failure):
  AssertEqual neomake#GetMaker('non-existent'), {}
  AssertEqual g:neomake_test_messages[-1], [0, 'Invalid maker name: non-existent', {}]
  AssertEqual neomake#GetMaker('nonexistent'), {}
  AssertEqual g:neomake_test_messages[-1], [0, 'Maker not found: nonexistent', {}]

Execute (Neomake with unknown maker):
  Neomake doesnotexist
  AssertEqual g:neomake_test_messages[-1], [0, 'Maker not found: doesnotexist', {}]

Execute (neomake#GetMaker uses defined errorformat with makeprg):
  Save &errorformat
  let &errorformat = '%Gcustom'
  AssertEqual neomake#GetMaker('makeprg', '').errorformat, '%Gcustom'

Execute (neomake#GetMaker uses defaults from b:/g:):
  Save g:neomake_test_remove_invalid_entries
  Save b:neomake_test_remove_invalid_entries

  let maker = {'name': 'test'}

  " Default.
  AssertEqual neomake#GetMaker(maker).remove_invalid_entries, 0,
  \ "default is ok"

  let maker.remove_invalid_entries = 1
  AssertEqual neomake#GetMaker(maker).remove_invalid_entries, 1
  let g:neomake_test_remove_invalid_entries = 2
  AssertEqual neomake#GetMaker(maker).remove_invalid_entries, 2
  let b:neomake_test_remove_invalid_entries = 3
  AssertEqual neomake#GetMaker(maker).remove_invalid_entries, 3

Execute (neomake#GetMaker uses defaults from b:/g: based on maker):
  Save g:neomake_test_remove_invalid_entries

  let maker = {'name': 'test'}
  AssertEqual neomake#GetMaker(maker).remove_invalid_entries, 0
  let maker.remove_invalid_entries = 1
  AssertEqual neomake#GetMaker(maker).remove_invalid_entries, 1
  let g:neomake_test_remove_invalid_entries = 2
  AssertEqual neomake#GetMaker(maker).remove_invalid_entries, 2
  let b:neomake_test_remove_invalid_entries = 3
  AssertEqual neomake#GetMaker(maker).remove_invalid_entries, 3

Execute (neomake#Make in file mode with no filetype and no makers):
  AssertEqual &ft, ''
  AssertEqual neomake#Make(1, []), []
  AssertEqual g:neomake_test_messages[-1], [3, 'Nothing to make: no enabled makers.', {}]

Execute (neomake#Make in project mode with no filetype and no makers):
  Save makeprg
  let &makeprg = 'sh -c "sleep 0.1"'
  let job_ids = neomake#Make(0, [])
  if neomake#has_async_support()
    AssertEqual len(job_ids), 1
    let jobs = neomake#GetJobs()
    let jobs_by_ids = neomake#GetJobs(job_ids)
    let job_by_id = neomake#GetJob(job_ids[0])
    AssertEqual len(jobs), 1
    AssertEqual jobs, [job_by_id]
    AssertEqual jobs, jobs_by_ids
    AssertEqual job_by_id.maker.name, 'makeprg'
  else
    AssertEqual len(job_ids), 0
  endif

  NeomakeTestsWaitForFinishedJobs

Execute (neomake#GetMaker from g:neomake_foo_maker):
  let g:neomake_custom_maker = {
    \ 'exe': 'my-exe'
    \ }
  let maker = neomake#GetMaker('custom')
  AssertEqual maker.exe, 'my-exe'
  AssertEqual maker.name, 'custom'

Execute (neomake#GetMaker without name):
  let maker = neomake#GetMaker({})
  AssertEqual maker.exe, 'unnamed_maker'
  AssertEqual maker.name, 'unnamed_maker'

Execute (neomake#CancelJob):
  if NeomakeAsyncTestsSetup()
    let job_id = neomake#Sh("sh -c 'while true; do sleep 0.1; done'")
    AssertEqual neomake#CancelJob(job_id), 1

    " The job is still in the table, therefore 'E900: Invalid job id'.
    AssertThrows neomake#CancelJob(job_id)
    AssertNeomakeMessage 'Stopping job', 3, neomake#GetJob(job_id)

    NeomakeTestsWaitForFinishedJobs
    AssertEqual neomake#CancelJob(job_id), 0
    AssertNeomakeMessage 'CancelJob: job not found: '.job_id, 0
  endif

Execute (neomake#CancelJob with invalid job):
  if NeomakeAsyncTestsSetup()
    let job_id = neomake#Sh("sh -c 'sleep 1'")
    let jobinfo = neomake#GetJob(job_id)
    if has('nvim')
      call jobstop(job_id)
    else
      let vim_job = jobinfo.vim_job
      call job_stop(vim_job)
      " Wait for the job to be stopped really, but without sleep to not trigger
      " the exit handler yet (which would remove the job).
      let c = 1000
      while c > 0 && job_status(vim_job) ==# 'run'
        let c -= 1
      endwhile
    endif
    let ret = neomake#CancelJob(job_id)
    Assert len(neomake#GetJobs()), 'There are jobs (1)'
    if has('nvim')
      " Vim returns 1 via job_stop usually.
      AssertEqual ret, 0, "CancelJob (1) returned ".ret
    else
      AssertNeomakeMessageAbsent 'exit: job not found: '.job_id
    endif
    AssertNeomakeMessage 'Stopping job', 3, jobinfo
    if has('nvim')
      AssertNeomakeMessage 'jobstop failed: Vim(call):E900: Invalid job id', 2, jobinfo
    else
      try
        AssertNeomakeMessage 'job_stop: job was not running anymore', 2, jobinfo
      catch
        " Happens at least on Vim 8.0.69.
        if !has('patch-8.0.70')
          throw v:exception.' (might be a flaky test)'
        else
          Log v:exception.' (ignoring potentially flaky test)'
        endif
      endtry
    endif
    Assert len(neomake#GetJobs()), 'There are jobs'

    let ret = neomake#CancelJob(job_id)
    if has('nvim')
      " Vim returns 1 via job_stop usually.
      AssertEqual ret, 0, "CancelJob (2) returned ".ret
    endif
    AssertNeomakeMessage 'Stopping job', 3, jobinfo

    AssertNeomakeMessageAbsent 'exit: job was canceled.', 3
    NeomakeTestsWaitForNextMessage
    AssertNeomakeMessage 'exit: job was canceled.', 3
    let ret = neomake#CancelJob(job_id)
    AssertEqual ret, 0, "CancelJob (3) returned ".ret
    AssertNeomakeMessage 'CancelJob: job not found: '.job_id, 0
  endif

Execute (neomake#CancelJob! with invalid job):
  if NeomakeAsyncTestsSetup()
    let job_id = neomake#Sh("sh -c 'sleep 1'")
    let jobinfo = neomake#GetJob(job_id)
    if has('nvim')
      call jobstop(job_id)
    else
      let vim_job = jobinfo.vim_job
      call job_stop(vim_job)
    endif
    let ret = neomake#CancelJob(job_id, 1)
    Assert !len(neomake#GetJobs()), 'There are no jobs'
    if has('nvim')
      " Vim returns 1 via job_stop usually.
      AssertEqual ret, 0, "CancelJob (1) returned ".ret
    endif
    AssertNeomakeMessageAbsent 'exit: job not found: '.job_id
    AssertNeomakeMessage 'Stopping job', 3, jobinfo
    if has('nvim')
      AssertNeomakeMessage 'jobstop failed: Vim(call):E900: Invalid job id', 2, jobinfo
    endif
    AssertEqual neomake#GetJobs(), []

    AssertEqual neomake#CancelJob(job_id), 0
    AssertNeomakeMessageAbsent 'Stopping job', 3, jobinfo

    NeomakeTestsWaitForFinishedJobs
    AssertNeomakeMessageAbsent 'exit: job not found: '.job_id
    NeomakeTestsWaitForNextMessage

    AssertNeomakeMessage 'exit: job not found: '.job_id, 1
    AssertEqual neomake#CancelJob(job_id, 1), 0
    AssertNeomakeMessage 'CancelJob: job not found: '.job_id, 0
  endif

Execute (Reports exit status: 7):
  call neomake#Sh("sh -c 'exit 7'")
  let exit_msg = 'sh: sh -c ''exit 7'': completed with exit code 7.'
  if neomake#has_async_support()
    let jobinfo = neomake#GetJobs()[-1]
    NeomakeTestsWaitForFinishedJobs
    AssertNeomakeMessage 'exit: sh: sh -c ''exit 7'': 7', 3, jobinfo
    AssertNeomakeMessage exit_msg, 3, jobinfo
  else
    " XXX: jobinfo gets used in messages, but is hard to get here, so we do not
    "      compare it.
    AssertNeomakeMessage 'exit: sh: sh -c ''exit 7'': 7', 3
    AssertThrows AssertNeomakeMessage exit_msg, 3
    AssertEqual g:vader_exception, 'Vim(call):E121: Undefined variable: exit_msg'
  endif

Execute (neomake#Sh: job_id):
  let job_id = neomake#Sh('true')
  if neomake#has_async_support()
    Assert job_id >= 0, 'Correct job_id for async.'
    NeomakeTestsWaitForFinishedJobs
  else
    Assert job_id == -1, 'Correct job_id for sync.'
  endif

Execute (neomake#CompleteJobs):
  if neomake#has_async_support()
    AssertEqual neomake#CompleteJobs(), ''
    let job_ids = [neomake#Sh('sleep 1')]
    AssertEqual neomake#CompleteJobs(), job_ids[0].': sh: sleep 1'
    let job_ids += [neomake#Sh('sleep 2')]
    AssertEqual neomake#CompleteJobs(), printf(
      \ "%d: sh: sleep 1\n%d: sh: sleep 2", job_ids[0], job_ids[1])
    NeomakeCancelJobs
    NeomakeTestsWaitForFinishedJobs
  endif

Execute (neomake#Make handles invalid cwd):
  let maker = {
      \ 'name': 'custom_maker',
      \ 'cwd': '/doesnotexist',
      \ }
  call neomake#Make(1, [maker])
  NeomakeTestsWaitForFinishedJobs
  AssertNeomakeMessage "custom_maker: could not change to maker's cwd (/doesnotexist): Vim(cd):E344: Can't find directory \"/doesnotexist\" in cdpath", 0

Execute (Neomake picks up custom maker correctly):
  let g:neomake_c_lint_maker = {
    \ 'exe': 'echo',
    \ 'args': ['%:p', '--foo', 'bar'],
    \ 'append_file': 0,
    \ 'errorformat': '%f:%l:%c: %m',
    \ }
  file file1
  let fname = expand('%:p')
  Save &filetype
  set filetype=c

  Neomake lint
  if neomake#has_async_support()
    AssertNeomakeMessage "Starting async job: ['echo', '".fname."', '--foo', 'bar']"
    NeomakeTestsWaitForFinishedJobs
  else
    AssertNeomakeMessage "Starting: echo '".fname."' '--foo' 'bar'"
  endif
